plugins {
	id "fabric-loom" version "1.11-SNAPSHOT"
	id "application"
}

version = project.version
group = project.maven_group

base {
	archivesName = project.name
}

application {
	mainClass = project.main_class
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	testImplementation "net.fabricmc:fabric-loader-junit:${project.loader_version}"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	
	modImplementation include("org.yaml:snakeyaml:${project.snakeyaml_version}")
}

test {
	useJUnitPlatform()
}

processResources {
	def props = [
		"version": project.version
	]
	inputs.properties(props)
	filesMatching("fabric.mod.json") {
		expand(props)
	}
	from("LICENSE") {
		into(".")
	}
}

tasks.javadoc {
	options.links("https://maven.fabricmc.net/docs/yarn-1.21.1+build.3/")
	options.links("https://maven.fabricmc.net/docs/fabric-loader-0.17.3/")
	options.links("https://javadoc.io/doc/org.yaml/snakeyaml/latest/")
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName
}